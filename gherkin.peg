package gherkin

type gherkinPeg Peg {
  gherkinPegBase

  buf1 string
  buf2 string
  buf3 string
  buftags []string
  bufcmt string
}

Begin <- 
  Feature?
  OS !.

Feature <- 
  Tags 'Feature:' WS* <UntilLineEnd?>{{buf1}} <>{{buf2}} LineEnd
  ( (WS* !('Background:' / 'Scenario:' / 'Scenario Outline:') <UntilLineEnd>{{++buf2}} LineEnd {p.buf2 = p.buf2 + "\n" })+ )?
  { p.beginFeature(trimWS(p.buf1), trimWSML(p.buf2), p.buftags); p.buftags = nil } 
  ( Background / Scenario / Outline / BlankLine )*
  { p.endFeature() }

Background <- 
  Tags 'Background:' WS* <UntilLineEnd?>{{buf1}} LineEnd
  { p.beginBackground(p.buf1, p.buftags); p.buftags = nil }
  Step*
  { p.endBackground() } 

Scenario <- 
  Tags 'Scenario:' WS* <UntilLineEnd?>{{buf1}} LineEnd
  { p.beginScenario(p.buf1, p.buftags); p.buftags = nil }
  Step* 
  { p.endScenario() }

Outline <- 
  Tags 'Scenario Outline:' WS* <UntilLineEnd?>{{buf1}} LineEnd
  { p.beginOutline(p.buf1, p.buftags); p.buftags = nil } 
  Step*
  OutlineExamples?
  { p.endOutline() } 

OutlineExamples <-
  OS 'Examples:' LineEnd
  { p.beginOutlineExamples() } 
  Table?
  { p.endOutlineExamples() } 

Step <- 
  Tags <('Given' / 'When' / 'Then' / 'And' / 'Or' / 'But')>{{buf1}}
  WS* <UntilLineEnd>{{buf2}} LineEnd
  { p.beginStep(p.buf1, p.buf2) } 
  StepArgument?
  { p.endStep() }

StepArgument
  <- Table
   / PyString

PyString <-
  (WS* NL)* <WS*> PyStringQuote NL
  { p.beginPyString(buffer[begin:end]) }
  (!(WS* PyStringQuote) PyStringLine)*
  WS* PyStringQuote LineEnd
  { p.endPyString() }

PyStringQuote <- '\"' '\"' '\"'

PyStringLine <-
  < UntilNL NL >
  { p.bufferPyString(buffer[begin:end]) }

Table <-
  { p.beginTable() }
  TableRow+
  { p.endTable() }

TableRow <-
  { p.beginTableRow() }
  OS '|' TableCell+ LineEnd
  { p.endTableRow() }

TableCell <-
  <( [^\r\n|]+ )> '|'
  { p.beginTableCell(); p.endTableCell(buffer[begin:end]) }

Tags <-
  (Tag+ WS* LineEnd?)* OS

Tag <-
  OS '@' <( Word )>{{[]buftags}}

Word <-
  [^\r\n\t "#]+

EscapedChar <- '\\' . 

QuotedString <- 
  '"' ( EscapedChar / [^\n\\"]+ )+ '"'

UntilLineEnd <-
  ( EscapedChar / [^\n\\"#]+ / QuotedString )+

LineEnd <- 
  WS* LineComment? ( NL / !. )

LineComment <- 
  '#' < [^\n]* >
  { p.bufcmt = buffer[begin:end] }

BlankLine <- (WS LineEnd)+

OS <- (NL/WS)*

WS <- (' ' / '\t')

UntilNL <- [^\n]*

NL <- ('\n' / '\r' / '\r\n')

